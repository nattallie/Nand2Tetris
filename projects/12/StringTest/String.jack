// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {

    field Array s;
    field int max_len;
    field int cur_len;

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int maxLength) {
        if (maxLength = 0) {
            let maxLength = 1;
        }
        let max_len = maxLength;
        let cur_len = 0;
        let s = Array.new(maxLength);
        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        do s.dispose();
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return cur_len;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        if (j < cur_len) {
            return s[j];
        }
        return 0;
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        if (j < cur_len) {
            let s[j] = c;
        }
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        let s[cur_len] = c;
        let cur_len = cur_len + 1;
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        if (cur_len > 0) {
            let cur_len = cur_len - 1;
        }
        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        var int res, i, sign;
        let res = 0;
        let sign = 1;

        if (s[0] = 45) {
            let i = i + 1;
            let sign = -sign;
        }
        while ((i < cur_len) & (s[i] > 47) & (s[i] < 58)) {
            let res = res * 10;
            let res = res + s[i] - 48;
            let i = i + 1;
        }

        return (res * sign);
    }

    method void rec_setInt(int val) {
        var int last_dig;
        if (val < 10) {
            do appendChar(val + 48);
        } else {
            let last_dig = val - ((val / 10) * 10);
            do rec_setInt((val/10));
            do appendChar(last_dig + 48);
        }

        return;
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        var int ind;

        let ind = cur_len - 1;

        while (~(ind < 0)) {
            do eraseLastChar();
            let ind = ind - 1;
        }

        if (val < 0) {
            do appendChar(45);
            let val = -val;
        }

        do rec_setInt(val);

        return;
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }
}
